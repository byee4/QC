SAMPLES = ["4226_CLIP1_downsampled", "4226_CLIP2_downsampled"]


rule all:
    input:
        "plots/quals.svg"


rule bwa_index:
    input:
        "inputs/hg19.fa"
    output:
        "inputs/hg19.fa.amb",
        "inputs/hg19.fa.ann",
        "inputs/hg19.fa.pac",
        "inputs/hg19.fa.bwt",
        "inputs/hg19.fa.sa"
    conda:
        "envs/bwa.yaml"
    shell:
        "bwa index {input}"
        
        
rule bwa_map:
    input:
        "inputs/hg19.fa.amb",
        "inputs/hg19.fa.ann",
        "inputs/hg19.fa.pac",
        "inputs/hg19.fa.bwt",
        "inputs/hg19.fa.sa",
        reference="inputs/hg19.fa",
        fastq="inputs/{sample}.fastq"
    output:
        "mapped_reads/{sample}.bam"
    conda:
        "envs/bwa.yaml"
    shell:
        "bwa mem {input.reference} {input.fastq} | samtools view -Sb - > {output}"


rule samtools_sort:
    input:
        "mapped_reads/{sample}.bam"
    output:
        "sorted_reads/{sample}.bam"
    conda:
        "envs/samtools.yaml"
    shell:
        "samtools sort -T sorted_reads/{wildcards.sample} "
        "-O bam {input} > {output}"


rule samtools_index:
    input:
        "sorted_reads/{sample}.bam"
    output:
        "sorted_reads/{sample}.bam.bai"
    conda:
        "envs/samtools.yaml"
    shell:
        "samtools index {input}"


rule bcftools_call:
    input:
        fa="inputs/hg19.fa",
        bam=expand("sorted_reads/{sample}.bam", sample=SAMPLES),
        bai=expand("sorted_reads/{sample}.bam.bai", sample=SAMPLES)
    output:
        "calls/all.vcf"
    shell:
        "bcftools mpileup -f {input.fa} {input.bam} | "
        "bcftools call -mv - > {output}"


rule plot_quals:
    input:
        "calls/all.vcf"
    output:
        "plots/quals.svg"
    conda:
        "envs/python3.yaml"
    script:
        "scripts/plot-quals.py"
